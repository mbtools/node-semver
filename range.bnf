/* This is the grammar used by the npm semver package in EBNF */
/* https://www.npmjs.com/package/semver */

<valid_range> ::=
        <range>
      | <range> <space>+ "||" <space>+ <valid_range>

<range> ::=
        <hyphenated_ranges>
      | <list_of_ranges>

<hyphenated_ranges> ::=
        <partial_version> <space>+ "-" <space>+ <partial_version>

<list_of_ranges> ::=
        <simple_range>
      | <simple_range> <space>+ <list_of_ranges>

<simple_range> ::=
        <full_version>
      | <partial_version>
      | <primitive_range>
      | <tilde_range>
      | <caret_range>

<full_version> ::=
        <prefix>? <valid_semver>

<partial_version> ::=
        <prefix>? <partial_semver> <qualifier>?

<partial_semver> ::=
        <placeholder>        ( "." <placeholder> )?     ( "." <placeholder> )?
      | <numeric_identifier> ( "." <placeholder> )?     ( "." <placeholder> )?
      | <numeric_identifier>   "." <numeric_identifier> ( "." <placeholder> )?

<primitive_range> ::=
        <gtlt> <space>* <partial_version>

<tilde_range> ::=
        "~" ">"? <space>* <partial_version>

<caret_range> ::=
        "^" <space>* <partial_version>

<comparator> ::=
        <gtlt> <space>* <full_version>

<prefix> ::=
        "v" 

<placeholder> ::=
        "x" | "X" | "*"

<gtlt> ::=
        "<" | "<=" | "=" | ">=" | ">"

<space> ::=
        " " | "\n" | "\r" | "\t"

/* The following is equivalent to the semver v2 grammar */
/* https://semver.org */

<valid_semver> ::=
        <version_core>
      | <version_core> "-" <pre_release>
      | <version_core> "+" <build>
      | <version_core> "-" <pre_release> "+" <build>

<version_core> ::=
        <major> "." <minor> "." <patch>

<major> ::=
        <numeric_identifier>

<minor> ::=
        <numeric_identifier>

<patch> ::=
        <numeric_identifier>

<qualifier> ::=
        "-" <pre_release>
      | "+" <build>
      | "-" <pre_release> "+" <build>

<pre_release> ::=
        <dot_separated_pre_release_identifiers>

<dot_separated_pre_release_identifiers> ::=
        <pre_release_identifier>
      | <pre_release_identifier> "." <dot_separated_pre_release_identifiers>

<build> ::=
        <dot_separated_build_identifiers>

<dot_separated_build_identifiers> ::=
        <build_identifier>
      | <build_identifier> "." <dot_separated_build_identifiers>

<pre_release_identifier> ::=
        <alphanumeric_identifier>
      | <numeric_identifier>

<build_identifier> ::=
        <alphanumeric_identifier>
      | <digits>

<alphanumeric_identifier> ::=
        <non_digit>
      | <non_digit> <identifier_characters>
      | <identifier_characters> <non_digit>
      | <identifier_characters> <non_digit> <identifier_characters>

<numeric_identifier> ::=
        "0"
      | <positive_digit>
      | <positive_digit> <digits>

<identifier_characters> ::=
        <identifier_character>+

<identifier_character> ::=
        <digit>
      | <non_digit>

<non_digit> ::=
        <letter> | "-"

<digits> ::=
        <digit>+

<digit> ::=
        "0" | <positive_digit>

<positive_digit> ::=
        [1-9]

<letter> ::=
        [A-Z] | [a-z]